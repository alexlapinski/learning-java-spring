buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE")
        classpath("gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.20.1")
    }
}

group = "alexlapinski"

apply plugin: "java"
apply plugin: "idea"
apply plugin: "com.palantir.docker"
apply plugin: "com.palantir.docker-run"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

bootJar {
    baseName = "gs-rest-service"
    version = "0.1.0"
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(["DEPENDENCY": "dependency"])
}

dockerRun {
    name "springboot-example"
    image "${project.group}/${bootJar.baseName}"
    ports "8080:8080"
}
